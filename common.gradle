allprojects {
    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "http://google.bintray.com/googlevr"
        }
        flatDir {
            dirs '../../gearvrf-libs', '../../../GearVRf/GVRf/Framework/framework/build/outputs/aar'
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"

    buildTypes {
        debug {
            resValue 'string', 'app_name', System.getProperty("appName")
        }
        release {
            resValue 'string', 'app_name', System.getProperty("appName")
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 19
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    System.getProperty("appName") + ".apk")
        }
    }

    // ignore the x86 and arm-v8 files from the google vr libraries
    packagingOptions {
        exclude 'lib/x86/libgvr.so'
        exclude 'lib/arm64-v8a/libgvr.so'
        if (!rootProject.hasProperty("ARM64")) {
            exclude 'lib/arm64-v8a/*.so'
        }
    }
}

task copyOculusSignature(type: Copy) {
    if (rootProject.hasProperty("OCULUS_SIGS_DIRECTORY")) {
        copy {
            from rootProject.property("OCULUS_SIGS_DIRECTORY")
            into 'src/main/assets'
            include 'oculussig_*'
        }
    }
}

preBuild {}.dependsOn copyOculusSignature

/*
To use local dependencies add the following line in a global gradle properties file
eg. ~/.gradle/gradle.properties:

useLocalDependencies=true

 */
if(!rootProject.hasProperty("useLocalDependencies")) {
    rootProject.ext.useLocalDependencies=false;
} else {
    if(useLocalDependencies=="true") {
        rootProject.ext.useLocalDependencies=true
    } else {
        rootProject.ext.useLocalDependencies=false
    }
}

ext.gearvrfVersion='3.1.1-SNAPSHOT'
project.ext.jomlVersion = "1.9.1-SNAPSHOT"

dependencies {
    compile "org.joml:joml-android:${jomlVersion}"
    if(rootProject.useLocalDependencies) {
        compile(name: 'framework-debug', ext: 'aar')
    } else {
        compile "org.gearvrf:framework:$gearvrfVersion"
    }

    if (rootProject.hasProperty("only_daydream")) {
        if(rootProject.useLocalDependencies) {
            compile(name: 'backend_daydream-debug', ext: 'aar')
        } else {
            compile "org.gearvrf:backend_daydream:$gearvrfVersion"
        }
        compile 'com.google.vr:sdk-base:1.10.0'
        compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
    } else {
        if (rootProject.hasProperty("no_oculus")) {
            compile(name: 'backend-debug', ext: 'aar')
        } else { //default oculus+daydream
            if(rootProject.useLocalDependencies) {
                compile(name: 'backend_daydream-debug', ext: 'aar')
                compile(name: 'backend_oculus-debug', ext: 'aar')
            } else {
                compile "org.gearvrf:backend_daydream:$gearvrfVersion"
                compile "org.gearvrf:backend_oculus:$gearvrfVersion"
            }
            compile 'com.google.vr:sdk-base:1.10.0'
            compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
        }
    }
}
