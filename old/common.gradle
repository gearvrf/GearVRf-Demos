allprojects {
    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "http://google.bintray.com/googlevr"
        }
        flatDir {
            dirs '../../../../GearVRf/GVRf/gearvrf-libs'
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"

    buildTypes {
        debug {
            debuggable = true
            jniDebuggable = true
            resValue 'string', 'app_name', System.getProperty("appName")
        }
        release {
            signingConfig signingConfigs.debug
            resValue 'string', 'app_name', System.getProperty("appName")
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 19
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    System.getProperty("appName") + ".apk")
        }
    }

    // ignore the x86 files from the google vr libraries
    packagingOptions {
        exclude 'lib/x86/libgvr.so'
        if (!rootProject.hasProperty("ARM64")) {
            exclude 'lib/arm64-v8a/*.so'
        } else {
            exclude 'lib/armeabi-v7a/*.so'
        }
    }
}

task copyOculusSignature(type: Copy) {
    if (rootProject.hasProperty("OCULUS_SIGS_DIRECTORY")) {
        copy {
            from rootProject.property("OCULUS_SIGS_DIRECTORY")
            into 'src/main/assets'
            include 'oculussig_*'
        }
    }
}

preBuild {}.dependsOn copyOculusSignature

/*
To use local dependencies add the following line in a global gradle properties file
eg. ~/.gradle/gradle.properties:

useLocalDependencies=true

 */
if(!rootProject.hasProperty("useLocalDependencies")) {
    rootProject.ext.useLocalDependencies=false;
} else {
    if(useLocalDependencies=="true") {
        rootProject.ext.useLocalDependencies=true
    } else {
        rootProject.ext.useLocalDependencies=false
    }
}

ext.gearvrfVersion='3.2'
project.ext.jomlVersion = "1.9.1-SNAPSHOT"

if(rootProject.useLocalDependencies) {
    dependencies {
        compile 'com.google.code.gson:gson:2.7'
        compile "org.joml:joml-android:${jomlVersion}"
        debugCompile(name: 'framework-debug', ext: 'aar')
        releaseCompile(name: 'framework-release', ext: 'aar')

        if (rootProject.hasProperty("only_daydream")) {
            debugCompile(name: 'backend_daydream-debug', ext: 'aar')
            releaseCompile(name: 'backend_daydream-release', ext: 'aar')
            compile 'com.google.vr:sdk-base:1.30.0'
        } else {
            if (rootProject.hasProperty("no_oculus")) {
                debugCompile(name: 'backend-debug', ext: 'aar')
                releaseCompile(name: 'backend-release', ext: 'aar')
            } else { //default oculus+daydream
                debugCompile(name: 'backend_daydream-debug', ext: 'aar')
                releaseCompile(name: 'backend_daydream-release', ext: 'aar')
                compile 'com.google.vr:sdk-base:1.30.0'
                debugCompile(name: 'backend_oculus-debug', ext: 'aar')
                releaseCompile(name: 'backend_oculus-release', ext: 'aar')
            }
        }
    }
} else if(rootProject.hasProperty("buildGvrfAndDemos")) {
    dependencies {
        compile 'com.google.code.gson:gson:2.7'
        compile "org.joml:joml-android:${jomlVersion}"
        compile project (':framework')

        if (rootProject.hasProperty("only_daydream")) {
            compile project (':backend_daydream')
            compile 'com.google.vr:sdk-base:1.30.0'
        } else {
            if (rootProject.hasProperty("no_oculus")) {
                compile project (':backend')
            } else { //default oculus+daydream
                compile project(':backend_oculus')
                compile project(':backend_daydream')
                compile 'com.google.vr:sdk-base:1.30.0'
            }
        }
    }
} else {
    dependencies {
        compile 'com.google.code.gson:gson:2.7'
        compile "org.joml:joml-android:${jomlVersion}"
        compile "org.gearvrf:framework:$gearvrfVersion"

        if (rootProject.hasProperty("only_daydream")) {
            compile "org.gearvrf:backend_daydream:$gearvrfVersion"
            compile 'com.google.vr:sdk-base:1.30.0'
        } else { //default oculus+daydream
            compile "org.gearvrf:backend_daydream:$gearvrfVersion"
            compile 'com.google.vr:sdk-base:1.30.0'
            compile "org.gearvrf:backend_oculus:$gearvrfVersion"
        }
    }
}

