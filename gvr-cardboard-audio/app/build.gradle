apply plugin: 'com.android.application'

System.setProperty("appName", "gvr-cardboard-audio")

if(file("../../common.gradle").exists()) {
    apply from: '../../common.gradle'
}
if(file("../../../../GearVRf-Demos/common.gradle").exists()) {
    apply from: '../../../../GearVRf-Demos/common.gradle'
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'src/main/libs'
        }
    }
}

android {
    defaultConfig {
        applicationId "org.gearvrf.sample.gvrcardboardaudio"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // no auto generation of Android.mk
        jniLibs.srcDir 'libs' // pre-compiled libraries
    }

    // ignore the x86 and arm-v8 files from the google audio libraries
    packagingOptions {
        exclude 'lib/x86/libvraudio_engine.so'
        exclude 'lib/arm64-v8a/libvraudio_engine.so'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile fileTree(dir: 'src/main/libs', include: ['*.jar'])
    compile(name: 'audio', ext: 'aar')

    if (!findGoogleVRCommon()) {
        compile(name: 'google-vr-common', ext: 'aar')
    }
}

boolean findGoogleVRCommon() {
    for (entry in project.configurations.compile.getDependencies()) {
        if (entry.name.contentEquals("google-vr-common")) {
            return true
        }
    }
    return false
}

